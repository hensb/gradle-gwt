// plugins
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'jetty'

// variables
if( System.env.HOME == null )
	throw new GradleException('Environment variable HOME not set.')
String home = "$System.env.HOME"

// repositories
repositories { 
	mavenCentral() 

	add(new org.apache.ivy.plugins.resolver.SshResolver()) {
		name = 'repos'
		publishPermissions = '0666'
		addIvyPattern 'www/web/repos/[organisation]/[module]/ivys/ivy-[revision].xml'
		addArtifactPattern 'www/web/repos/[organisation]/[module]/[type]s/[organisation]-[artifact]-[revision].[ext]'
		user = 'maven'
		host = "win2014"
		keyFile = file(home + "/.ssh/id_rsa")
	}
}

// eclipse relevant stuff
eclipse {
	project {
		natures 'com.google.gwt.eclipse.core.gwtNature'
		natures 'com.google.appengine.eclipse.core.gaeNature'
		natures 'org.eclipse.wst.common.project.facet.core.nature'

		buildCommand 'com.google.gdt.eclipse.core.webAppProjectValidator'
		buildCommand 'com.google.gwt.eclipse.core.gwtProjectValidator'
		buildCommand 'org.eclipse.wst.common.project.facet.core.builder'
		buildCommand 'com.google.appengine.eclipse.core.gaeProjectChangeNotifier'
		buildCommand 'com.google.appengine.eclipse.core.projectValidator'
		buildCommand 'com.google.appengine.eclipse.core.enhancerbuilder'
	}
	classpath {
		containers 'com.google.gwt.eclipse.core.GWT_CONTAINER'
		containers 'com.google.appengine.eclipse.core.GAE_CONTAINER'

		// be sure to exclude this from sourceSets (see below)
		defaultOutputDir file('src/main/webapp/WEB-INF/classes')
	}
}

// project dependencies
dependencies {

	providedCompile 'com.google.gwt:gwt-user:2.4.0'
	providedCompile 'com.google.gwt:gwt-dev:2.4.0'

	// Needed for GWT compile and at runtime for RequestBuilder
	// Specify two artifacts as workaround for GRADLE-1934
	compile('javax.validation:validation-api:1.0.0.GA') {
		artifact {
			name = 'validation-api'
			type = 'jar'
		}
		artifact {
			name = 'validation-api'
			type = 'jar'
			classifier = 'sources'
		}
	}

	compile 'com.typesafe.akka:akka-remote_2.10:2.1.1' 
	compile 'de.info2.ub:CommunicationInterface:1.0.5-SNAPSHOT'
	compile 'de.info2.ub:MessageHeaders:1.0.3-SNAPSHOT'
	compile 'de.info2.ub:WuesyphusDomain:0.9-SNAPSHOT'
	compile 'de.info2.ub:akka-comm-facade:1.0.2-RELEASE'
	
	compile 'com.claudiushauptmann:gwt-recaptcha:1.0.0.Beta2'
	compile 'net.tanesha.recaptcha4j:recaptcha4j:0.0.7'
	compile 'com.typesafe.akka:akka-remote_2.10:2.1.1'
	compile 'com.typesafe:config:1.0.0'
	compile 'javax.mail:mail:1.4.4'

}

// specify project layout
sourceSets {
	main {
		java {
			srcDir './src'
			// exclude webapp-dir
			exclude 'main/webapp/'
		}
		java { srcDir './test' }
	}
}

// gwtCompile task
task compileGWT (dependsOn: classes, type: JavaExec) {
	buildDir = "${project.buildDir}/gwt"
	extraDir = "${project.buildDir}/extra"

	inputs.source sourceSets.main.java.srcDirs
	inputs.dir sourceSets.main.output.resourcesDir
	outputs.dir buildDir

	doFirst {
		file(buildDir).mkdirs()
	}

	main = 'com.google.gwt.dev.Compiler'

	classpath {
		[
			sourceSets.main.java.srcDirs,
			// Java source
			sourceSets.main.output.resourcesDir,
			// Generated resources
			sourceSets.main.output.classesDir,
			// Generated classes
			sourceSets.main.compileClasspath,       // Deps
		]
	}


	args =
			[
				'de.info2.ub.auftragsformular.Wuebtrag',
				// Your GWT module
				'-war',
				buildDir,
				'-logLevel',
				'INFO',
				'-localWorkers',
				'6',
				'-compileReport',
				'-extra',
				extraDir,
				// '-draftCompile' // Speeds up compile with 25%
			]

	maxHeapSize = '256M'
}

// be sure to exec compileGWT prior to packaging stuff into a webapp
war.dependsOn compileGWT
war { from compileGWT.buildDir }

// we point jetty to our new webapp
jettyRunWar.webApp file(war.archivePath)
